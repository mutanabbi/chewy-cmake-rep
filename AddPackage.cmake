# Copyright 2011-2013 by Alex Turbov <i.zaufi@gmail.com>
#
# Command to add a target to make a specified package.
#
# Example:
#
#   add_package(
#       NAME libzencxx
#       SUMMARY "C++11 reusable code collection"
#       DESCRIPTION "Header only libraries"
#       PACKAGE_VERSION 0ubuntu1
#       DEPENDS boost
#       SET_DEFAULT_CONFIG_CPACK
#     )
#
# TODO Add `cpack' programs detection
#
# TODO Add param to specify more .deb specific params like:
#   CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA
#
# TODO Add help about generic usage and about set_common_package_options() particularly
#

include(CMakeParseArguments)

set(_ADD_PACKAGE_MODULE_BASE_DIR "${CMAKE_CURRENT_LIST_DIR}")


function(set_common_package_options)
    set(options)
    set(
        one_value_args
            PROJECT_VERSION
            BUILD_FLAVOUR
            VENDOR_NAME
            VENDOR_CONTACT
            LICENSE_FILE
            README_FILE
            PACKAGE_INSTALL_PREFIX
      )
    set(multi_value_args)
    cmake_parse_arguments(
        set_common_package_options
        "${options}"
        "${one_value_args}"
        "${multi_value_args}"
        ${ARGN}
      )

    # Check mandatory parameters
    # 0) project version
    if(NOT set_common_package_options_PROJECT_VERSION)
        message(FATAL_ERROR "Project version is not given")
    endif()
    # 1) vendor name
    if(NOT set_common_package_options_VENDOR_NAME)
        message(FATAL_ERROR "Vendor name is not given")
    endif()
    # 2) vendor contact
    if(NOT set_common_package_options_VENDOR_CONTACT)
        message(FATAL_ERROR "Vendor contact is not given")
    endif()
    # Other options are optional...
    if(NOT set_common_package_options_BUILD_FLAVOUR)
        set(set_common_package_options_BUILD_FLAVOUR "")
    endif()
    if(NOT set_common_package_options_README_FILE)
        if(EXISTS "${CMAKE_SOURCE_DIR}/README")
            set(set_common_package_options_README_FILE "${CMAKE_SOURCE_DIR}/README")
        elseif(EXISTS "${CMAKE_SOURCE_DIR}/README.md")
            set(set_common_package_options_README_FILE "${CMAKE_SOURCE_DIR}/README.md")
        else()                                              # TODO Check other files?
            set(set_common_package_options_README_FILE "")
        endif()
    endif()
    if(NOT set_common_package_options_LICENSE_FILE)
        if(EXISTS "${CMAKE_SOURCE_DIR}/LICENSE")
            set(set_common_package_options_LICENSE_FILE "${CMAKE_SOURCE_DIR}/LICENSE")
        else()
            set(set_common_package_options_LICENSE_FILE "")
        endif()
    endif()
    if(NOT set_common_package_options_PACKAGE_INSTALL_PREFIX)
        # Reasonable default for all packages in any distro
        set(set_common_package_options_PACKAGE_INSTALL_PREFIX "/usr")
    endif()

    # Generate a common package options file 2b included from
    # all particilar package control files generated by add_package()
    configure_file(
        ${_ADD_PACKAGE_MODULE_BASE_DIR}/CPackCommonPackageOptions.cmake.in
        ${CMAKE_BINARY_DIR}/CPackCommonPackageOptions.cmake
      )
endfunction()


function(add_package)
    set(options SET_DEFAULT_CONFIG_CPACK)
    set(one_value_args NAME SUMMARY DESCRIPTION PACKAGE_VERSION SECTION HOMEPAGE)
    set(multi_value_args DEPENDS REPLACES PRE_BUILD CONTROL_FILES)
    cmake_parse_arguments(add_package "${options}" "${one_value_args}" "${multi_value_args}" ${ARGN})

    # Make sure set_common_package_options() was called before
    if(NOT EXISTS ${CMAKE_BINARY_DIR}/CPackCommonPackageOptions.cmake)
        message(FATAL_ERROR "set_common_package_options() must be called before any of add_package()")
    endif()

    # Check mandatory parameters
    # 0) package name
    if(NOT add_package_NAME)
        message(FATAL_ERROR "Package name is not given")
    else()
        set(PACKAGE_NAME "${add_package_NAME}")
        string(TOUPPER "${add_package_NAME}" pkg_name)
        string(REPLACE "-" "_" pkg_name "${pkg_name}")
        set(${pkg_name}_PACKAGE "${add_package_NAME}" PARENT_SCOPE)
    endif()
    # 1) package description
    if(NOT add_package_SUMMARY OR NOT add_package_DESCRIPTION)
        message(FATAL_ERROR "Package description and/or summary is not provided")
    else()
        set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${add_package_SUMMARY}")
        set(CPACK_PACKAGE_DESCRIPTION "${add_package_DESCRIPTION}")
    endif()

    # Check optional parameters
    if (add_package_SET_DEFAULT_CONFIG_CPACK)
        set(config_file CPackConfig.cmake)
    else()
        set(config_file CPack-${add_package_NAME}.cmake)
    endif()
    # package version
    if(NOT add_package_PACKAGE_VERSION)
        set(CPACK_PACKAGE_VERSION "0ubuntu1")
    else()
        set(CPACK_PACKAGE_VERSION "${add_package_PACKAGE_VERSION}")
    endif()
    # package homepage
    if(add_package_HOMEPAGE)
        set(CPACK_DEBIAN_PACKAGE_HOMEPAGE "${add_package_HOMEPAGE}")
    endif()
    # dependencies list
    if(add_package_DEPENDS)
        # Form a comma separated list of dependencies from a cmake's list
        string(REPLACE ";" ", " CPACK_DEBIAN_PACKAGE_DEPENDS "${add_package_DEPENDS}")
    endif()
    # replaces list
    if(add_package_REPLACES)
        # Form a comma separated list of dependencies from a cmake's list
        string(REPLACE ";" ", " CPACK_DEBIAN_PACKAGE_REPLACES "${add_package_REPLACES}")
    endif()
    # package control files
    if(add_package_CONTROL_FILES)
        # Form a comma separated list of dependencies from a cmake's list
        set(CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA "${add_package_CONTROL_FILES}")
    endif()

    # Generate a package specific cpack's config file to be used
    # at custom execution command...
    configure_file(
        ${_ADD_PACKAGE_MODULE_BASE_DIR}/CPackPackageConfig.cmake.in
        ${CMAKE_BINARY_DIR}/${config_file}
      )
    # Add target to create the specified package using just the generated config file
    add_custom_target(
        ${add_package_NAME}-package
        COMMAND cpack --config ${config_file}
        DEPENDS
            ${add_package_PRE_BUILD}
            ${CMAKE_BINARY_DIR}/CPackCommonPackageOptions.cmake
        COMMENT "Makeing package ${add_package_NAME}"
      )
endfunction()

# X-Chewy-RepoBase: https://raw.github.com/mutanabbi/chewy-cmake-rep/master/
# X-Chewy-Path: AddPackage.cmake
# X-Chewy-Version: 3.1
# X-Chewy-Description: Add a target to make a .deb package
# X-Chewy-AddonFile: CPackCommonPackageOptions.cmake.in
# X-Chewy-AddonFile: CPackProjectConfig.cmake.in
# X-Chewy-AddonFile: CPackPackageConfig.cmake.in
